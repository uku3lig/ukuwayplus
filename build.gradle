plugins {
    id "fabric-loom" version "1.4-SNAPSHOT"
    id "io.freefair.lombok" version "8.3"

    id "com.modrinth.minotaur" version "2.+"
}

version = "${project.mod_version}+mc${project.minecraft_version}"
group = project.maven_group

loom {
    accessWidenerPath = file("src/main/resources/ukuwayplus.accesswidener")
}

repositories {
    mavenCentral()
    maven {
        url "https://maven.uku3lig.net/releases"
    }
    maven {
        url = "https://maven.terraformersmc.com/"
    }
    maven {
        url = "https://pkgs.dev.azure.com/djtheredstoner/DevAuth/_packaging/public/maven/v1"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation fabricApi.module("fabric-key-binding-api-v1", project.fabric_api_version)
    modImplementation fabricApi.module("fabric-lifecycle-events-v1", project.fabric_api_version)

    modApi "net.uku3lig:ukulib:${project.ukulib_version}"

    include implementation("io.github.CDAGaming:DiscordIPC:0.8.1")
    include implementation("com.kohlschutter.junixsocket:junixsocket-common:2.8.1")
    include implementation("com.kohlschutter.junixsocket:junixsocket-native-common:2.8.1")

    modRuntimeOnly "com.terraformersmc:modmenu:7.2.2"
    modRuntimeOnly "me.djtheredstoner:DevAuth-fabric:1.1.2"
}

processResources {
    inputs.property "version", project.version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
}

base {
    archivesName = project.archives_base_name
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archives_base_name}" }
    }
}

var release_type = "release"
if (version.containsIgnoreCase("alpha")) release_type = "alpha"
else if (version.containsIgnoreCase("beta")) release_type = "beta"

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "iSgroavL"
    versionNumber = project.version
    versionType = release_type
    uploadFile = remapJar // With Loom, this MUST be set to `remapJar` instead of `jar`!
    gameVersions = [project.minecraft_version] // Must be an array, even with only one version
    loaders = ["fabric", "quilt"]
    dependencies {
        required.project "Y8uFrUil"
    }
}
